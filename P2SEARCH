       IDENTIFICATION DIVISION.
       PROGRAM-ID.  P2SEARCH.
       AUTHOR. DOMBROWSKI.

       ENVIRONMENT DIVISION.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT STUDENT-FILE ASSIGN TO 'SEARCH'.

           SELECT MAJOR-REPORT ASSIGN TO 'INDEXSRH'.


       DATA DIVISION.
       FILE SECTION.

       FD  STUDENT-FILE RECORDING MODE IS F.
       01                              PIC X(80).

       FD  MAJOR-REPORT RECORDING MODE IS F.
       01  PRINT-LINE                  PIC X(132).


       WORKING-STORAGE SECTION.

       01  WORKING-VARIABLES.
           05  EOF-STUDENT-WS          PIC X(3)    VALUE 'NO'.

       01  STUDENT-REC-IN.
           05  STUDENT-NAME-IN         PIC X(20).
           05  STUDENT-CODE-IN         PIC X(4).


       01  REPORT-HEADER.
           05  FILLER                  PIC X(20)   VALUE SPACES.
           05  FILLER                  PIC X(60)   VALUE
               'This is the Student Major Report using SEARCH'.

       01  COLUMN-HEADER1.
           05  FILLER                  PIC X(1)    VALUE SPACES.
           05  FILLER                  PIC X(25)
                                       VALUE 'Student Name'.
           05  FILLER                  PIC X(26)
                                       VALUE 'Student Major'.
           05  FILLER                  PIC X(28)
                                       VALUE 'Student Code Errors'.

       01  DETAIL-LINE.
           05  FILLER                  PIC X(1)  VALUE SPACES.
           05  STUDENT-NAME-OUT        PIC X(25).
           05  STUDENT-MAJOR-OUT       PIC X(31).
           05  STUDENT-CODE-OUT        PIC X(23).

       01  MAJOR-SETUP.
           05  FILLER                  PIC X(20) VALUE '2400Science'.
           05  FILLER                  PIC X(20) VALUE '2600Drama'.
           05  FILLER                  PIC X(20) VALUE '2800Language'.
           05  FILLER                  PIC X(20) VALUE '1300Marketing'.
           05  FILLER                  PIC X(20) VALUE '1400Management'.
           05  FILLER                  PIC X(20) VALUE '2200History'.
           05  FILLER                  PIC X(20) VALUE '1100Computing'.
           05  FILLER                  PIC X(20) VALUE '1200Accounting'.
           05  FILLER                  PIC X(20) VALUE '1500Finance'.
           05  FILLER                  PIC X(20) VALUE '2000English'.

       01  MAJORS REDEFINES MAJOR-SETUP.
           05  MAJOR-TABLE  OCCURS 10 TIMES INDEXED BY MAJOR-INDEX.
               10  MAJOR-CODE-TAB  PIC X(4).
               10  MAJOR-NAME-TAB  PIC X(16).


       PROCEDURE DIVISION.

       100-MAINLINE SECTION.
           PERFORM 200-OPEN.
           PERFORM 300-PROCESS UNTIL EOF-STUDENT-WS = 'YES'.
           PERFORM 900-CLOSE.
           STOP RUN.


       200-OPEN SECTION.
           OPEN INPUT STUDENT-FILE OUTPUT MAJOR-REPORT.
           PERFORM 250-READ-RECORD.
           PERFORM 500-HEADER.


       250-READ-RECORD SECTION.
           READ STUDENT-FILE INTO STUDENT-REC-IN
              AT END MOVE 'YES' TO EOF-STUDENT-WS
           END-READ.


       300-PROCESS SECTION.

           SET MAJOR-INDEX TO 1.
           SEARCH MAJOR-TABLE

               AT END MOVE '***BAD MAJOR CODE***' TO STUDENT-MAJOR-OUT
                    MOVE STUDENT-CODE-IN  TO STUDENT-CODE-OUT

               WHEN MAJOR-CODE-TAB (MAJOR-INDEX) = STUDENT-CODE-IN
                    MOVE MAJOR-NAME-TAB (MAJOR-INDEX)
                         TO STUDENT-MAJOR-OUT
                    MOVE SPACES TO STUDENT-CODE-OUT

           END-SEARCH.

           MOVE  STUDENT-NAME-IN TO STUDENT-NAME-OUT.
           MOVE  DETAIL-LINE     TO  PRINT-LINE.
           WRITE PRINT-LINE AFTER 1.
           PERFORM 250-READ-RECORD.


       500-HEADER SECTION.

           WRITE PRINT-LINE FROM REPORT-HEADER AFTER PAGE.

           WRITE PRINT-LINE FROM COLUMN-HEADER1 AFTER 3.

           MOVE SPACES TO PRINT-LINE.
           WRITE PRINT-LINE AFTER 2.


       900-CLOSE SECTION.
           CLOSE STUDENT-FILE MAJOR-REPORT.
